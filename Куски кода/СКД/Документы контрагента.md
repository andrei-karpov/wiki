Отчет в поле элемента формы справочника с расшифровкой, и возможностью открыть по ссылке другую базу (начиная с версии платформы 8.3.5)
Отчет для старой платформы, поэтому не использовался БСП.
РТ_АдресДанныеРасшифровки - это реквизит формы типа строка

```bsl

&НаСервере
Процедура ВывестиДокументыКонтрагента()
 
 Если Объект.Ссылка.Пустая() Тогда
  Возврат; 
 КонецЕсли;
 
 СхемаДокументыКонтрагенту = РеквизитФормыВЗначение("Объект").ПолучитьМакет("РТ_ДокументыПоКонтрагенту");
 Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
 URLСхемы = ПоместитьВоВременноеХранилище(СхемаДокументыКонтрагенту, Новый УникальныйИдентификатор());
 ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
 Компоновщик.Инициализировать(ИсточникНастроек);
 Компоновщик.ЗагрузитьНастройки(СхемаДокументыКонтрагенту.ВариантыНастроек ["Основной"].Настройки);
 Настройки = Компоновщик.ПолучитьНастройки();

 Запрос = Новый Запрос;
 Запрос.Текст = 
 "ВЫБРАТЬ
 | ВидыВнутреннихДокументов.Ссылка КАК ВидДокумента
 |ИЗ
 | Справочник.ВидыВнутреннихДокументов КАК ВидыВнутреннихДокументов
 |ГДЕ
 | ВидыВнутреннихДокументов.РТ_ТипДоговора = ЗНАЧЕНИЕ(Перечисление.РТ_ТипДоговора.Соглашение)
 | И ВидыВнутреннихДокументов.ЯвляетсяДоговором";
 
 СписокДопСоглашений = Новый СписокЗначений;
 СписокДопСоглашений.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДокумента"));
 
 Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
  "ДопСоглашения", СписокДопСоглашений);
 
 Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
  "ЗаказПоставщику", РТ_МодификацияКонфигурацииВызовСервера.СсылкаПоИмени("ЗаказПоставщику"));
 
 Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
  "Корреспондент", Объект.Ссылка);
 
 КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
 ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
 МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаДокументыКонтрагенту, Настройки, ДанныеРасшифровки,, Тип("ГенераторМакетаКомпоновкиДанных"));
 ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
 ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки, Истина); 
 ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
 ПроцессорВывода.УстановитьДокумент(РТ_ДокументыКонтрагента);
 ПроцессорВывода.Вывести(ПроцессорКомпоновки);
    РТ_АдресДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
  
КонецПроцедуры

&НаКлиенте
Процедура РТ_ДокументыКонтрагентаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
 СтандартнаяОбработка = Ложь;
 Если НЕ ЗначениеЗаполнено(РТ_АдресДанныеРасшифровки) Тогда
  Возврат; 
 КонецЕсли;
 РезультатОбработки = ОбработкаРасшировкиНаСервере(Расшифровка);  
 Если РезультатОбработки = Неопределено Тогда
  Возврат; 
 КонецЕсли; 
 Если РезультатОбработки.Операция = "Открыть" Тогда
  ПоказатьЗначение(, РезультатОбработки.Значение); 
 ИначеЕсли РезультатОбработки.Операция = "ПерейтиПоСсылке" Тогда     
  ПерейтиПоНавигационнойСсылке(РезультатОбработки.Значение);
 КонецЕсли; 
  
КонецПроцедуры  

&НаСервере
Функция ОбработкаРасшировкиНаСервере(Расшифровка)
 
 ДанныеРасшифровки = ПолучитьИзВременногоХранилища(РТ_АдресДанныеРасшифровки);   
 ВыбранноеЗначение = ДанныеРасшифровки.Элементы [Расшифровка].ПолучитьПоля()[0].Значение;
 Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
  Возврат Неопределено; 
 КонецЕсли;   
 
 Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ВидДокумента") = РТ_МодификацияКонфигурацииВызовСервера.СсылкаПоИмени("ЗаказПоставщику") Тогда
  Возврат Новый Структура("Операция, Значение", "Открыть", ВыбранноеЗначение);
 КонецЕсли;
 
 Запрос = Новый Запрос;
 Запрос.Текст = 
 "ВЫБРАТЬ
 | СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта
 |ИЗ
 | РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК СвязиОбъектовИнтегрированныхСистем
 |ГДЕ
 | СвязиОбъектовИнтегрированныхСистем.СсылкаНаОбъектДО = &Документ";
 Запрос.УстановитьПараметр("Документ", ВыбранноеЗначение);
 Выборка = Запрос.Выполнить().Выбрать();
 Если Выборка.Следующий() Тогда   
  ПредставлениеГуид = СтрЗаменить(Выборка.ИДВнешнегоОбъекта, "-", Символы.ПС);
  ОбратныйГуид = СтрПолучитьСтроку(ПредставлениеГуид, 4) 
        + СтрПолучитьСтроку(ПредставлениеГуид, 5) 
        + СтрПолучитьСтроку(ПредставлениеГуид, 3) 
        + СтрПолучитьСтроку(ПредставлениеГуид, 2) 
        + СтрПолучитьСтроку(ПредставлениеГуид, 1);
        
  ИмяБазыERP = РТ_ДополнительныеНастройкиПовтИсп.ЗначениеНастройкиПоИмени("ИмяБазыERP", "ERP");
  ИмяСервераERP = РТ_ДополнительныеНастройкиПовтИсп.ЗначениеНастройкиПоИмени("ИмяСервераERP", "server");
  СтрокаЗапуска = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("e1c://server/%1/%2#e1cib/data/%3?ref=%4", 
   ИмяСервераERP, ИмяБазыERP, "Документ.ЗаказПоставщику", ОбратныйГуид); 
  
  Возврат Новый Структура("Операция, Значение", "ПерейтиПоСсылке", СтрокаЗапуска);
 КонецЕсли;  
 
 Возврат Новый Структура("Операция, Значение", "Открыть", ВыбранноеЗначение);
 
КонецФункции

&НаКлиенте
Процедура РезультатЗапускаПриложенияОбработкаРасширофвки(КодОтвета, ДопПараметры = Неопределено) Экспорт
 // пусто 
КонецПроцедуры

```
