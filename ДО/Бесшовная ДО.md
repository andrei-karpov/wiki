Бесшовная интеграция представляет из себя опубликованный веб-сервис на стороне Документооборота. "DM", в нем прописаны все основные структуры, для использования в других системах.

**"Рекомендации для Документооборота 2.1.26.2"**

в модуле ОбработкаЗапросовXDTOДокументы располагается основное заполнение структур, которые будут переданы в связанную систему
Примеры редактирования

```bsl

&После("ПолучитьДанныеВнутреннегоДокумента")
Процедура WD_ПолучитьДанныеВнутреннегоДокумента(Документ, ОбъектXDTO, НаборКолонок = Неопределено, ДляОбмена = Ложь)
	
	ОбъектXDTO.WD_ГрафикОтпусков = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ГрафикОтпусковТ");
	
	Для Каждого СтрокаГрафика Из Документ.WD_ГрафикОтпусков Цикл
		
		СтрокаXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ГрафикОтпусковТ.Строка");
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Сотрудник", СтрокаГрафика.Сотрудник);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ДатаНачала", СтрокаГрафика.ДатаНачала);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ДатаОкончания", СтрокаГрафика.ДатаОкончания);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "КоличествоДней", СтрокаГрафика.КоличествоДней);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Согласован", 
						СтрокаГрафика.Статус = ПредопределенноеЗначение("Перечисление.WD_СтатусГрафикаОтпусков.Согласован"));
		
		ОбъектXDTO.WD_ГрафикОтпусков.Rows.Добавить(СтрокаXDTO);
		
	КонецЦикла;
	
	//<WD КАРПОВ А.В. 01.07.2021 MD-14.04
	 
	ОбъектXDTO.WD_ШтатКСокращению = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ШтатКСокращениюТ");
	
	Для Каждого СтрокаСокращение Из Документ.WD_ШтатКСокращению Цикл
		
		СтрокаXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ШтатКСокращениюТ.Строка");
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Сотрудник", СтрокаСокращение.Сотрудник);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Должность", СтрокаСокращение.Должность);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ДатаСокращения", СтрокаСокращение.ДатаСокращения);
		МожноСократить = Ложь;
		Если ЗначениеЗаполнено(СтрокаСокращение.Документ) Тогда
			РешениеСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаСокращение.Документ, "WD_РешениеСотрудника"); 
			Если РешениеСотрудника <> ПредопределенноеЗначение("Перечисление.WD_РешениеСотрдникаПриСокращении.ПредоставитьДокументыПозволяющиеСохранитьМестоРаботы") Тогда
				МожноСократить = Истина;
			КонецЕсли; 
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаСокращение.Сотрудник) Тогда 	
			МожноСократить = Истина;
		КонецЕсли; 
		
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "МожноСократить", МожноСократить);
		
		ОбъектXDTO.WD_ШтатКСокращению.Rows.Добавить(СтрокаXDTO);
		
	КонецЦикла;
	
	//WD КАРПОВ А.В.> 
	
	//<WD КАРПОВ А.В. 28.09.2021 HR-12.06
	 	 
	ОбъектXDTO.WD_ДеталиПоездки = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ДеталиПоездкиТ");
	ОбъектXDTO.Бит_ЦельКомандировки = Документ.Бит_ЦельКомандировки;
	Для Каждого СтрокаДеталиПоездки Из Документ.WD_ДеталиПоездки Цикл
		
		СтрокаXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ДеталиПоездкиТ.Строка");
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ПринимающаяСторона", СтрокаДеталиПоездки.ПринимающаяСторона);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ДатаВыезда", СтрокаДеталиПоездки.ДатаВыезда);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ДатаПрибытия", СтрокаДеталиПоездки.ДатаПрибытия);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "МестоНазначения", Строка(СтрокаДеталиПоездки.МестоНазначения));
		ОбъектXDTO.WD_ДеталиПоездки.Rows.Добавить(СтрокаXDTO);
		
	КонецЦикла;
	
	//WD КАРПОВ А.В.> 

	//<WD КАРПОВ А.В. 04.10.2021
	ОбъектXDTO.WD_ДатаЗавершенияКонтракта = Документ.WD_ДатаЗавершенияКонтракта;
	//WD КАРПОВ А.В.>  
	
	//<WD КАРПОВ А.В. 16.10.2021 A-13.04
	ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(ОбъектXDTO, "WD_ДатаНачалаОсмотров", Документ.WD_ДатаНачалаОсмотров);
	ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(ОбъектXDTO, "WD_ДатаОкончанияОсмотров", Документ.WD_ДатаОкончанияОсмотров);
	ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(ОбъектXDTO, "WD_МедицинскаяОрганизация", Документ.WD_МедицинскаяОрганизация);
	
	ОбъектXDTO.WD_ПлановыеМедосмотры = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ПлановыеМедосмотрыТ");
	
	Для Каждого СтрокаОсмотра Из Документ.WD_ПлановыеМедосмотры Цикл
		СтрокаXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ПлановыеМедосмотрыТ.Строка");
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Сотрудник", СтрокаОсмотра.Сотрудник);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ПлановаяДатаОсмотра", СтрокаОсмотра.ПлановаяДатаОсмотра);
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "КомментарийСотрудника", СтрокаОсмотра.КомментарийСотрудника);
		ОбъектXDTO.WD_ПлановыеМедосмотры.Rows.Добавить(СтрокаXDTO);
	КонецЦикла;
	
	ОбъектXDTO.WD_ПлановыеДиспансеризации = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ПлановыеДиспансеризацииТ");
	
	Для Каждого СтрокаДиспансеризации Из Документ.WD_ПлановыеДиспансеризации Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаДиспансеризации.РешениеСотрудника) Тогда
			Продолжить;	
		КонецЕсли; 	
		СтрокаXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("WD_ПлановыеДиспансеризацииТ.Строка");
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "Сотрудник", СтрокаДиспансеризации.Сотрудник);
		Попытка
			ДатаДиспансеризации = Дата(СокрЛП(СтрокаДиспансеризации.КомментарийСотрудника) + " 00:00:00");
		Исключение
			ДатаДиспансеризации = СтрокаДиспансеризации.ПлановаяДатаДиспансеризации;
		КонецПопытки;
		ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "ПлановаяДатаДиспансеризации", ДатаДиспансеризации);
		Если СтрокаДиспансеризации.РешениеСотрудника = Перечисления.WD_РешениеСотрудникиПриДиспансеризации.Отказано Тогда
			ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "РезультатДиспансеризации", "Отказано");
		Иначе
			ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(СтрокаXDTO, "РезультатДиспансеризации", "Согласен");
		КонецЕсли; 
		ОбъектXDTO.WD_ПлановыеДиспансеризации.Rows.Добавить(СтрокаXDTO);
	КонецЦикла;

	//WD КАРПОВ А.В.>  
	
	//<WD КАРПОВ А.В. 01.07.2021 HR-12.11
	ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(ОбъектXDTO, "WD_ПериодНетрудоспособностиС", Документ.WD_ПериодНетрудоспособностиС);
	ОбработкаЗапросовXDTO.ЗаполнитьСвойствоXDTOИзРеквизита(ОбъектXDTO, "WD_ПериодНетрудоспособностиПо", Документ.WD_ПериодНетрудоспособностиПо);
	//WD КАРПОВ А.В.>  
	
КонецПроцедуры	
//WD КАРПОВ А.В.> 

&Перед("ЗаполнитьДанныеВнутреннегоДокумента")
Процедура WD_ЗаполнитьДанныеВнутреннегоДокумента(ДокументОбъект, ОбъектXDTO)
	//<WD КАРПОВ А.В. 16.10.2021 A-13.04
	ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(ДокументОбъект.WD_ДатаНачалаОсмотров, ОбъектXDTO, "WD_ДатаНачалаОсмотров");
	ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(ДокументОбъект.WD_ДатаОкончанияОсмотров, ОбъектXDTO, "WD_ДатаОкончанияОсмотров");
	ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(ДокументОбъект.WD_МедицинскаяОрганизация, ОбъектXDTO, "WD_МедицинскаяОрганизация");
	//WD КАРПОВ А.В.>  	
КонецПроцедуры



```
