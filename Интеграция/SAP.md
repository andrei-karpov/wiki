Пример, как можно подключиться и получить информацию из системы SAP, получение значения происходит через com соединение и специальный sap коннектор

```bsl

Перем SAP_R3;

Процедура ПримерИспользования()
 
 //ПЕРВЫЙ ПРИМЕР
 
 // Пример запроса к базе SAP R3
 ДатаНач = НачалоМесяца(ТекущаяДата());
 ДатаКон = КонецМесяца(ДатаНач);
 
 НастройкиОтбора = Новый Структура;
 НастройкиОтбора.Вставить("GJAHR", Формат(Год(ДатаКон),"ЧГ=0"));
 НастройкиОтбора.Вставить("BUKRS", "1205"); // Номер таблицы
 //BLDAT - Дата документа Период по дате документа
 НастройкиОтбора.Вставить("BLDAT_LOW", "01.01.2000");
 НастройкиОтбора.Вставить("BLDAT_HIGH", "31.12.9999");
 //BUDAT - Дата проводок, Период по дате проводки
 НастройкиОтбора.Вставить("BUDAT_LOW", Формат(ДатаНач, "ДФ=dd.MM.yyyy"));
 НастройкиОтбора.Вставить("BUDAT_HIGH", Формат(ДатаКон, "ДФ=dd.MM.yyyy"));
 НастройкиОтбора.Вставить("BELNR", НомерБухДокумента); // Номер документа
 
 ИменаТаблицДляЗапроса = Новый Массив;
 ИменаТаблицДляЗапроса.Добавить("OUT_DH11012");
 ИменаТаблицДляЗапроса.Добавить("OUT_DT11012");
 ИмяФункции = "ZFM_INV_OUTPUT_MTO";
 
 РезультатПоНесколькимТаблицам = РезультатЗапросаНесколькоТаблицSAP(ИмяФункции, ИменаТаблицДляЗапроса, НастройкиОтбора);

 // ВТОРОЙ ПРИМЕР
 
 ИмяФункции = "Z_RFC_TENDER_GET_MARA";        
 НастройкиОтбора = Новый Структура("LASTDATE, WERKS1", ДатаНач,"T051");
 ИмяТаблицы = "ZMARA"; 
 ДанныеОтветаSAP = РезультатЗапросаSAP(ИмяФункции, ИмяТаблицы, НастройкиОтбора);
 
КонецПроцедуры

Процедура УстановитьЗначениеОтбораSAP(ФункцияОбъектSAP, ИмяПоля, ЗначениеПоля)
 ЭлементОбъекта = ФункцияОбъектSAP.Exports(ИмяПоля);  
 ЭлементОбъекта.Value = ЗначениеПоля; 
КонецПроцедуры
  
Функция РезультатФункцииSAP(ИмяФункцииSAP, ИмяПоля, Отбор = Неопределено)
 ФункцияОбъектSAP = ComПодключениеSAP_R3().Add(ИмяФункцииSAP);
 ЗаполнитьОтборSAP(ФункцияОбъектSAP, Отбор);
 ОбъектОтвета = ФункцияОбъектSAP.imports(ИмяПоля);
 Если ФункцияОбъектSAP.Call = Истина Тогда
  Возврат ОбъектОтвета.value;
 Иначе
  Возврат Неопределено;
 КонецЕсли;
КонецФункции

Процедура ЗаполнитьОтборSAP(ФункцияОбъектSAP, Отбор = Неопределено)
 Если Отбор <> Неопределено Тогда
  Для Каждого ЭлементОтбора Из Отбор Цикл
   УстановитьЗначениеОтбораSAP(ФункцияОбъектSAP, ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
  КонецЦикла; 
 КонецЕсли;      
КонецПроцедуры

Функция РезультатЗапросаSAP(ИмяФункцииSAP, ИмяТаблицы, Отбор = Неопределено)
 ФункцияОбъектSAP = ComПодключениеSAP_R3().Add(ИмяФункцииSAP);
 ЗаполнитьОтборSAP(ФункцияОбъектSAP, Отбор); 
 ОбъектТаблицаSAP = ФункцияОбъектSAP.Tables(ИмяТаблицы);
 Если ФункцияОбъектSAP.Call = Истина Тогда
  Возврат ОбъектТаблицаSAP;
 Иначе
  Возврат Неопределено;
 КонецЕсли; 
КонецФункции

Функция РезультатЗапросаНесколькоТаблицSAP(ИмяФункцииSAP, СписокТаблицSAP, Отбор = Неопределено)
 ФункцияОбъектSAP = ComПодключениеSAP_R3().Add(ИмяФункцииSAP);
 ЗаполнитьОтборSAP(ФункцияОбъектSAP, Отбор);  
 Ответ = Новый Структура;
 Для Каждого ИмяТаблицы Из СписокТаблицSAP Цикл
  Ответ.Вставить(ИмяТаблицы, ФункцияОбъектSAP.Tables(ИмяТаблицы));
 КонецЦикла; 
 Если ФункцияОбъектSAP.Call = Истина Тогда
  Возврат Ответ;
 Иначе
  Возврат Неопределено;
 КонецЕсли; 
КонецФункции

Функция ТаблицаЗначенийИзТаблицыSAP(ОбъектТаблицаSAP)
 Таблица1С = ПустаяТаблицаЗначенийИзТаблицыSAP(ОбъектТаблицаSAP);
 Для НомерСтроки = 1 По ОбъектТаблицаSAP.RowCount Цикл
  НоваяСтрока = Таблица1С.Добавить();
  Для НомерКолонки = 1 По ОбъектТаблицаSAP.ColumnCount Цикл
   ИмяКолонки = ИмяКолонкиSAP(ОбъектТаблицаSAP, НомерКолонки);
   НоваяСтрока[ИмяКолонки] = ЗначениеПоля(ОбъектТаблицаSAP, НомерСтроки, ИмяКолонки);
  КонецЦикла; 
 КонецЦикла; 
 Возврат Таблица1С;
КонецФункции
 
Функция ПустаяТаблицаЗначенийИзТаблицыSAP(ОбъектТаблицаSAP)
 Таблица1С = Новый ТаблицаЗначений; 
 Для НомерКолонки = 1 По ОбъектТаблицаSAP.ColumnCount Цикл
  Таблица1С.Колонки.Добавить(ИмяКолонкиSAP(ОбъектТаблицаSAP, НомерКолонки));
 КонецЦикла; 
 Возврат Таблица1С;
КонецФункции
 
Функция ИмяКолонкиSAP(ОбъектТаблицаSAP, НомерКолонки)
 Возврат СокрЛП(ОбъектТаблицаSAP.Columns.Item(НомерКолонки).Name); 
КонецФункции

Функция ЗначениеПоля(ОбъектТаблицаSAP, НомерСтроки, ИмяКолонки)
 Возврат СокрЛП(ОбъектТаблицаSAP.Value(НомерСтроки, ИмяКолонки));
КонецФункции
 
Функция СоединениеАктивно()
 Возврат ComПодключениеSAP_R3().Connection.IsConnected = 1; 
КонецФункции

Функция ComПодключениеSAP_R3()
 Если SAP_R3 <> Неопределено Тогда
  Возврат SAP_R3;
 КонецЕсли;
 
 SAP_R3 = Новый COMОбъект("SAP.Functions");
 SAP_R3.LogFileName = "wdtflog.txt";
 SAP_R3.loglevel = 1;
 
 //Language = "RU";
 //tracelevel = 0; 
 
 SapConnection = SAP_R3.Connection;
 SapConnection.System = Система;
 SapConnection.ApplicationServer = СокрЛП(Имя_Сервера);
 SapConnection.User = СокрЛП(Имя_Пользователя);
 SapConnection.password = СокрЛП(Пароль);
 SapConnection.Client = СокрЛП(Client);
 SapConnection.SystemNumber = СокрЛП(SystemNumber);
 SapConnection.Language = СокрЛП(Language);
 SapConnection.tracelevel = tracelevel;
 // ПРИМЕР ЗАПОЛНЕНИЯ
 //Система = "RNP";
 //Language = "RU";
 //tracelevel = 0; 
 //Client = 400; 
 //Имя_Сервера = "/H/10.16.70.5/S/3297/H/sapas09"; 
 //Имя_Пользователя = "RFC_MATPRICE";
 //Пароль = "UNGSPRIC";
 //SystemNumber = "84"; 
 //BUKRS_GET="1205";

 Если СоединениеАктивно() И SapConnection.logon(0, False) <> Ложь Тогда
  Система = СокрЛП(SapConnection.System);
  Имя_Сервера = СокрЛП(SapConnection.ApplicationServer);
  Имя_Пользователя = СокрЛП(SapConnection.User);
  SystemNumber = СокрЛП(SapConnection.SystemNumber); 
  Client = СокрЛП(SapConnection.Client);
 Иначе
  SapConnection = Неопределено;
  SAP_R3 = Неопределено;
 КонецЕсли; 
 
 Возврат SAP_R3;
 
КонецФункции
```
